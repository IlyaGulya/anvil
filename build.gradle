buildscript {
  apply from: rootProject.file('dependencies.gradle')

  repositories {
    google()
    gradlePluginPortal()
    mavenCentral()
  }

  dependencies {
    classpath deps.android_gradle_plugin
    classpath deps.kotlin.dokka
    classpath deps.kotlin.gradle_plugin
    classpath deps.ktlint_plugin
    classpath deps.anvil_plugin
    classpath deps.maven_publishing_plugin
  }
}

println "Versions: " + [
    "Kotlin": ext.kotlinVersion,
    "Gradle": gradle.gradleVersion,
    "Kotlin use IR": ext.kotlinUseIR
]

allprojects {
  repositories {
    google()
    mavenCentral()
    // Required for publishing, Dokka needs org.jetbrains.kotlinx:kotlinx-html-jvm:0.7.2
    jcenter()
  }
}

subprojects {
  if (buildFile.exists()) {
    apply plugin: 'org.jlleitschuh.gradle.ktlint'

    ktlint {
      version = rootProject.ext.ktlintVersion
      verbose = true
    }
  }

  tasks.withType(Test).configureEach {
    testLogging {
      events 'passed', 'failed', 'skipped', 'standardOut', 'standardError'
      exceptionFormat 'FULL'
      showCauses true
      showExceptions true
      showStackTraces true
      showStandardStreams true
    }
  }

  tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  //noinspection UnnecessaryQualifiedReference
  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
      jvmTarget = JavaVersion.VERSION_1_8
      useIR = rootProject.ext.kotlinUseIR
      allWarningsAsErrors = rootProject.ext.makeWarningsErrors
      freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]

      boolean optInExperimental = project.name != "annotations"
          && project.name != "scopes"
          && project.name != "dagger-factories-only"
      if (optInExperimental) {
        freeCompilerArgs += "-Xopt-in=com.squareup.anvil.annotations.ExperimentalAnvilApi"
      }
    }
  }

  // TODO: fix obscure dependency substitution error
  // This is the ideal solution, but doesn't quite work for some reason. The workaround below is
  // needed to make it work for now.
  configurations.all {
    resolutionStrategy.dependencySubstitution {
      substitute module("com.squareup.anvil:annotations") with project(':annotations')
      substitute module("com.squareup.anvil:compiler") with project(':compiler')
      substitute module("com.squareup.anvil:compiler-api") with project(':compiler-api')
      substitute module("com.squareup.anvil:compiler-utils") with project(':compiler-utils')
    }
  }
  // This is an ugly workaround.
  def brokenProjects = [
      project(':integration-tests:code-generator-tests'),
      project(':integration-tests:dagger-factories-only'),
      project(':integration-tests:library'),
      project(':integration-tests:tests'),
      project(':sample:library'),
      project(':sample:app')
  ]
  brokenProjects.each { brokenProject ->
    brokenProject.configurations.whenObjectAdded {
      if (it.name == 'api') {
        if (brokenProject.path != ':integration-tests:dagger-factories-only' ||
            !rootProject.ext.generateDaggerFactoriesWithAnvil) {
          brokenProject.dependencies.add('api', project(':annotations'))
        }
      }
      if (it.name == 'kotlinCompilerPluginClasspath') {
        brokenProject.dependencies.add('kotlinCompilerPluginClasspath', project(':compiler'))
        brokenProject.dependencies.add('kotlinCompilerPluginClasspath', project(':compiler-api'))
        brokenProject.dependencies.add('kotlinCompilerPluginClasspath', project(':compiler-utils'))
      }
    }
  }
  // End ugly workaround.
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
